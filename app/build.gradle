buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.27.0'
        classpath 'com.github.triplet.gradle:play-publisher:1.2.0'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'com.github.triplet.play'


def loadExtraProperties(String fileName) {
    def props = new Properties()
    props.load(new FileInputStream(fileName))

    props.each { key, val ->
        project.setProperty(key, val)
    }
}


loadExtraProperties("treetracker.keys.properties")


android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file('keys.json')
        }
    }
    defaultConfig {
        applicationId "org.greenstand.android.TreeTracker"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 45
        versionName "1.2.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        playAccountConfig = playAccountConfigs.defaultAccountConfig
        buildConfigField "String", "DOS_ACCESS_KEY", "\"${dos_access_key}\""
        buildConfigField "String", "DOS_SECRET_KEY", "\"${dos_secret_key}\""
        buildConfigField "String", "TREETRACKER_CLIENT_ID", "\"${treetracker_client_id}\""
        buildConfigField "String", "TREETRACKER_CLIENT_SECRET", "\"${treetracker_client_secret}\""
    }
    buildTypes {
        release {
            minifyEnabled true
            resValue "string", "app_name", "Tree Tracker"
            buildConfigField "String", "GPS_ACCURACY", '"on"'
            buildConfigField 'String', 'BASE_URL', '"https://treetracker.org/api/mobile/"'
            buildConfigField 'String', 'DO_SPACES_ENDPOINT', '"nyc3"'
            buildConfigField 'String', 'DO_SPACES_BUCKET', '"treetracker-production"'
            buildConfigField 'String', 'ENABLE_FABRIC', '"true"'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "Tree Tracker(debug)"
            buildConfigField "String", "GPS_ACCURACY", '"on"'
            buildConfigField 'String', 'BASE_URL', '"http://test.treetracker.org/api/mobile/"'
            buildConfigField 'String', 'DO_SPACES_ENDPOINT', '"ams3"'
            buildConfigField 'String', 'DO_SPACES_BUCKET', '"treetracker-test"'
            buildConfigField 'String', 'ENABLE_FABRIC', '"true"'
        }
        dev {
            initWith(debug)
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "Tree Tracker(dev)"
            buildConfigField "String", "GPS_ACCURACY", '"off"'
            buildConfigField 'String', 'BASE_URL', '"http://dev.treetracker.org/api/mobile/"'
            buildConfigField 'String', 'DO_SPACES_ENDPOINT', '"nyc3"'
            buildConfigField 'String', 'DO_SPACES_BUCKET', '"treetracker-dev"'
            buildConfigField 'String', 'ENABLE_FABRIC', '"false"'
        }

        staging {
            initWith(debug)
            applicationIdSuffix ".staging"
            resValue "string", "app_name", "Tree Tracker(staging)"
            buildConfigField "String", "GPS_ACCURACY", '"on"'
            buildConfigField 'String', 'BASE_URL', '"http://staging.treetracker.org"'
            buildConfigField 'String', 'ENABLE_FABRIC', '"true"'
        }

    }
    productFlavors {

    }
    useLibrary 'org.apache.http.legacy'
}

repositories {
    maven {
        url "https://maven.google.com"
    }
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')

    implementation "androidx.appcompat:appcompat:${androidSupportVersion}"
    implementation "com.google.android.material:material:${androidSupportVersion}"
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation 'com.amazonaws:aws-android-sdk-core:2.2.22'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.2'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.27.0-eap13'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.27.0-eap13'

    api 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    api "com.squareup.retrofit2:retrofit:${retrofit2Version}"
    api "com.squareup.retrofit2:converter-gson:${retrofit2Version}"
    api 'com.jakewharton.timber:timber:4.6.1'
    api 'com.amazonaws:aws-android-sdk-s3:2.2.22'
    api('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }
    api 'com.google.firebase:firebase-core:16.0.6'



    implementation "androidx.legacy:legacy-support-v4:${androidSupportVersion}"
}
play {
    track = 'beta' // or 'rollout' or 'production' or 'alpha'
}

apply plugin: 'com.google.gms.google-services'

